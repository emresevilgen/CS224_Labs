##
## Program1.asm - prints out "hello world"
##
##	a0 - points to the string
##

#################################
#					 	#
#		text segment		#
#						#
#################################

	.text		
	.globl __start 

__start:		# execution starts here
	la $a0,str	# put string address into a0
	li $v0,4	# system call to print
	syscall		#   out a string

	li $v0,10  # system call to exit
	syscall	#    bye bye


#################################
#					 	#
#     	 data segment		#
#						#
#################################

	.data
str:	.asciiz "Hello Akifhan\n"
n:	.word	10

##
## end of file Program1.asm

##
## Program2.asm asks user for temperature in Celsius,
##  converts to Fahrenheit, prints the result.
##
##	v0 - reads in Celsius
##	t0 - holds Fahrenheit result
##	a0 - points to output strings
##

#################################
#					 	#
#		text segment		#
#						#
#################################

	.text		
	.globl __start	

__start:
	la $a0,prompt	# output prompt message on terminal
	li $v0,4	# syscall 4 prints the string
	syscall

	li $v0,5		# syscall 5 reads an integer
	syscall

	mul $t0,$v0,9	# to convert,multiply by 9,
	div $t0,$t0,5	# divide by 5, then
	add $t0,$t0,32	# add 32

	la $a0,ans1	# print string before result
	li $v0,4
	syscall

	move $a0,$t0	# print integer result
	li $v0,1		# using syscall 1
	syscall

	la $a0,endl	# system call to print
	li $v0,4		# out a newline
	syscall

	li $v0,10		# system call to exit
	syscall		#    bye bye


#################################
#					 	#
#     	 data segment		#
#						#
#################################

	.data
prompt:	.asciiz "Enter temperature (Celsius): "
ans1:	.asciiz "The temperature in Fahrenheit is "
endl:	.asciiz "\n"

##
## end of file Program2.asm

##
##	Program3.asm is a loop implementation
##	of the Fibonacci function
##        

#################################
#					 	#
#		text segment		#
#						#
#################################

	.text		
.globl __start
 
__start:		# execution starts here
	li $a0,7	# to calculate fib(7)
	jal fib		# call fib
	move $a0,$v0	# print result
	li $v0, 1
	syscall

	la $a0,endl	# print newline
	li $v0,4
	syscall

	li $v0,10
	syscall		# bye bye

#------------------------------------------------


fib:	move $v0,$a0	# initialise last element
	blt $a0,2,done	# fib(0)=0, fib(1)=1

	li $t0,0	# second last element
	li $v0,1	# last element

loop:	add $t1,$t0,$v0	# get next value
	move $t0,$v0	# update second last
	move $v0,$t1	# update last element
	sub $a0,$a0,1	# decrement count
	bgt $a0,1,loop	# exit loop when count=1
done:	jr $ra

#################################
#					 	#
#     	 data segment		#
#						#
#################################

	.data
endl:	.asciiz "\n"

##
## end of Program3.asm

	.data
enterA: .asciiz "\nEnter the a: "
enterB: .asciiz "\nEnter the b: "
enterC: .asciiz "\nEnter the c: "
result: .asciiz "\nThe result is: "

	.text
main:
	#Gets the number of elements
	li $v0, 4
	la $a0, enterA
	syscall
	
	li $v0, 5
	syscall
	move $s0, $v0
	
	#Gets the number of elements
	li $v0, 4
	la $a0, enterB
	syscall
	
	li $v0, 5
	syscall
	move $s1, $v0
	
	#Gets the number of elements
	li $v0, 4
	la $a0, enterC
	syscall
	
	li $v0, 5
	syscall
	move $s2, $v0
	
	mul $t0, $s0, $s1
	mul $t0, $t0, 4
	
	sub $t1, $s2, 2
	div $t1, $t1, 3
	
	div $t0, $t1
	mfhi $t3 #Remainder
	
	#Shows the result
	li $v0, 4
	la $a0, result
	syscall
	
	li $v0, 1
	move $a0, $t3
	syscall
	
	li $v0, 10
	syscall

	
	.data
enterNumberOfElements: .asciiz "\nEnter the number of elements: "
enterElement: .asciiz "\nEnter the element: "
menu: .asciiz "\n\n1.Find summation of numbers stored in the array which is greater than an input number.\n2.Find summation of numbers within a value range specified two numbers and display that value.\n3.Display the number of occurrences of the array elements divisible by a certain input number.\n4.Display number of unique numbers in the array.\n5.Quit.\nEnter the number of your selection: "
enterThreshold: .asciiz "\nEnter the threshold: "
result: .asciiz "\nThe result is: "
enterLowerBoundry: .asciiz "\nEnter the lower boundry: "
enterUpperBoundry: .asciiz "\nEnter the upper boundry: "
enterDivisor: .asciiz "\nEnter the divisor: "

	.text
main:
	#Gets the number of elements
	li $v0, 4
	la $a0, enterNumberOfElements
	syscall
	
	li $v0, 5
	syscall
	move $s0, $v0
	
	#Creates the array
	mul $t0, $s0, 4
	move $a0, $t0
	li $v0, 9
	syscall
	

	move $s1, $v0 	#First index of the array
	
	#Last index of the array
	mul $s2, $s0, 4  
	add $s2, $s2, $s1
	add $t0, $s1, $zero

getElements: #Gets the elements one by one adds into the array
	li $v0, 4
	la $a0, enterElement
	syscall

	li $v0, 5
	syscall
	
	sw $v0, ($t0)
	addi $t0, $t0, 4

	blt $t0, $s2, getElements
	
getMenuSelection: #Displays the menu and gets the selection
	li $v0, 4
	la $a0, menu
	syscall
	
	li $v0, 5
	syscall
	move $t0, $v0
	
	beq $t0, 1, sumGreater
	beq $t0, 2, sumWithinRange
	beq $t0, 3, numberOfDivisible
	beq $t0, 4, numberOfUniques
	beq $t0, 5, quit
	
	
sumGreater:
	#Gets the threshold
	li $v0, 4
	la $a0, enterThreshold
	syscall
	
	li $v0, 5
	syscall
	move $s3, $v0
	
	add $t0, $s1, $zero #First index
	li $t1, 0 #Result
	
checkBoundry:  
	bge $t0, $s2, sumGreaterExit
	lw $t2, ($t0)
	addi $t0, $t0, 4
	bgt $t2, $s3, addToSum
	j checkBoundry

addToSum:
	add $t1, $t1, $t2
	j checkBoundry
	
sumGreaterExit:
	#Shows the result
	li $v0, 4
	la $a0, result
	syscall
	
	li $v0, 1
	move $a0, $t1
	syscall
	
	j getMenuSelection

sumWithinRange:
	#Gets the lower boundry
	li $v0, 4
	la $a0, enterLowerBoundry
	syscall
	
	li $v0, 5
	syscall
	move $s3, $v0
	
	#Gets the upper boundry	
	li $v0, 4
	la $a0, enterUpperBoundry
	syscall
	
	li $v0, 5
	syscall
	move $s4, $v0
	
	add $t0, $s1, $zero #First index
	li $t1, 0 #Result
	
checkRange1:  
	bge $t0, $s2, sumWithinRangeExit
	lw $t2, ($t0)
	addi $t0, $t0, 4
	bgt $t2, $s3, checkRange2
	j checkRange1

checkRange2:
	blt $t2, $s4, addToSum2	
	j checkRange1

addToSum2:
	add $t1, $t1, $t2
	j checkRange1
	
sumWithinRangeExit:
	#Shows the result
	li $v0, 4
	la $a0, result
	syscall
	
	li $v0, 1
	move $a0, $t1
	syscall
	
	j getMenuSelection

numberOfDivisible:
	#Gets the divisor
	li $v0, 4
	la $a0, enterDivisor
	syscall
	
	li $v0, 5
	syscall
	move $s3, $v0
	
	add $t0, $s1, $zero #First index
	li $t1, 0 #Result
	
checkDivisible:
	bge $t0, $s2, numberOfDivisibleExit
	lw $t2, ($t0)
	addi $t0, $t0, 4
	
	div $t2, $s3
	mfhi $t3 #Remainder
	beq $t3, $zero, addToSum3
	j checkDivisible
	
addToSum3:
	addi $t1, $t1, 1
	j checkDivisible

numberOfDivisibleExit:
	#Shows the result
	li $v0, 4
	la $a0, result
	syscall
	
	li $v0, 1
	move $a0, $t1
	syscall
	
	j getMenuSelection

numberOfUniques:
	add $t0, $s1, $zero #First index
	li $t4, 0 #Result
	
firstLoop:
	bge $t0, $s2, firstLoopExit
	add $t1, $s1, $zero
	lw $t2, ($t0)

	
secondLoop:
	bge $t1, $t0, addToSum4
	lw $t3, ($t1)
	addi $t1, $t1, 4
	beq $t2, $t3, secondLoopExit
	j secondLoop

addToSum4:
	addi $t4, $t4, 1
	
secondLoopExit:
	addi $t0, $t0, 4
	j firstLoop
		
firstLoopExit:
	#Shows the result
	li $v0, 4
	la $a0, result
	syscall
	
	li $v0, 1
	move $a0, $t4
	syscall
	
	j getMenuSelection

quit:
	li $v0, 10
	syscall
